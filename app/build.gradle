apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 42
        versionName "3.4.4"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-dontobfuscate.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        beta {
            dimension "version"
            applicationId "com.faltenreich.diaguard.beta"
            resValue 'string', 'package_name', applicationId
            buildConfigField "boolean", "isCalculatorEnabled", "true"
        }
        store {
            dimension "version"
            applicationId "com.faltenreich.diaguard"
            resValue 'string', 'package_name', applicationId
            buildConfigField "boolean", "isCalculatorEnabled", "false"
        }
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            includeAndroidResources = true
        }
    }
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'org.apache.commons', module: 'commons-io'
}

def jUnit = '4.12'
def jUnitRunner = '1.1.1'
def robolectric = '4.3'
def espresso = '3.2.0'

def jodaTime = '2.10.3'
def ormLite = '5.1'
def butterKnife = '10.2.1'
def retrofit = '2.8.1'

dependencies {
    implementation project(':pdfjet')

    debugImplementation 'androidx.fragment:fragment-testing:1.2.3'

    testImplementation "junit:junit:$jUnit"
    testImplementation "org.robolectric:robolectric:$robolectric"
    testImplementation "androidx.test.ext:junit:$jUnitRunner"
    testImplementation "androidx.test.espresso:espresso-core:$espresso"
    testImplementation "androidx.test.espresso:espresso-contrib:$espresso"
    testImplementation "joda-time:joda-time:$jodaTime"

    androidTestImplementation "junit:junit:$jUnit"
    androidTestImplementation "androidx.test.ext:junit:$jUnitRunner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso"

    // FIXME: Updating com.google.android.material:material alters RecyclerView and breaks com.lapism:searchview
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'

    implementation "com.j256.ormlite:ormlite-core:$ormLite"
    implementation "com.j256.ormlite:ormlite-android:$ormLite"
    implementation "net.danlew:android.joda:$jodaTime"
    implementation 'org.greenrobot:eventbus:3.2.0'

    implementation "com.jakewharton:butterknife:$butterKnife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife"

    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'org.apache.commons:commons-text:1.1'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.0.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.opencsv:opencsv:3.10'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.fmsirvent:parallaxeverywhere:1.0.4'
    implementation 'com.lapism:searchview:4.0'
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
    implementation 'com.robinhood.ticker:ticker:2.0.2'
}

task applyVersionToReadme() {
    def android = project.extensions.findByName("android")
    def versionName = android.defaultConfig.versionName
    def string = 'img.shields.io/badge/Release-'
    def regex = "$string([0-9.]+)"
    def with = "$string$versionName"
    ant.replaceregexp(file: "${rootProject.projectDir}/README.md", match: regex, flags: 'g', replace: with)
}

assemble.dependsOn applyVersionToReadme
